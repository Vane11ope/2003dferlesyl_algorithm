;;;;;;;;main;;;;;;;;
'c'i
krz f1 2782070968    ; text
krz f1@ 1            ata f1 4
krz f1@ 4            ata f1 4
krz f1@ 2            ata f1 4
krz f1@ 4            ata f1 4
krz f1@ 3            ata f1 4
krz f1@ 5            ata f1 4
krz f1@ 9            ata f1 4
krz f1@ 3            ata f1 4
krz f1@ 4294967295   ata f1 4

krz f1 2782070920    ; pattern
krz f1@ 3            ata f1 4
krz f1@ 5            ata f1 4
krz f1@ 9            ata f1 4
krz f1@ 4294967295   ata f1 4

nta f5 4 krz f5@ 2782070920
nta f5 4 krz f5@ 2782070968
nta f5 4 inj f5@ xx bmsearch ata f5 12
krz xx f5@
;;;;;;;;main;;;;;;;;

;;;;;;;;BMsearch;;;;;;;;
'c'i
nll bmsearch
krz f1 f5+4@ ; text
krz f2 f5+8@ ; pattern

; f5+4294967204@ = textlen
nta f5 4 krz f5@ f1
nta f5 4 inj f5@ xx size ata f5 8
krz f5+4294967204@ f0

; f5+4294967200@ = patlen
nta f5 4 krz f5@ f2
nta f5 4 inj f5@ xx size ata f5 8
krz f5+4294967200@ f0

krz f0 2780000000 ; 2780000000 = skip
krz f3 0          ; f3 = i

nll loop1 ; for (i = 0; i < 10; ++i) skip[i] = patlen
fi f3 10 xolo malkrz xx interval1 ; if f3 >= 10 then go to next loop
krz f0@ f5+4294967200@
ata f0 4
ata f3 1
krz xx loop1

nll interval1
krz f3 0 ; i = 0

nll loop2 ; for (i = 0; i < patlen - 1; ++i) skip[pattern[i]] = patlen - i - 1;
krz f0 2780000000 ; skip
krz f1 f5+4294967200@ ; patlen
nta f1 1 ; patlen - 1
fi f3 f1 xolo malkrz xx interval2 ; if f3 >= (patlen - 1) then go to next loop
krz f2 f5+8@ ; pattern
dro f3 2 ata f2 f3 dto f3 2 ; pattern + (i << 2)
dro f2@ 2 ata f0 f2@ nta f1 f3 krz f0@ f1 dto f2@ 2 ; skip[pattern[i]] = patlen - i -1
ata f3 1
krz xx loop2

nll interval2
krz f3 f5+4294967200@ ; i
nta f3 1
krz f5+4294967196@ f3 ; save i

nll while1
fi f3 f5+4294967204@ xolo malkrz xx lus ; i >= textlen
krz f0 f5+4294967200@ ; j = patlen
nta f0 1 ; j = patlen - 1
krz f5+4294967192@ f0 ; save j

nll while2
krz f1 f5+4@ ; text
krz f2 f5+8@ ; pattern
dro f3 2 ata f1 f3 dto f3 2 ; text+i
dro f0 2 ata f2 f0 dto f0 2 ; pattern+j
fi f1@ f2@ niv malkrz xx out
fi f0 0 clo malkrz xx lus2
nta f3 1
nta f0 1
krz f5+4294967196@ f3 ; save i
krz f5+4294967192@ f0 ; save j
krz xx while2

nll out  ; i = i + max(skip[text[i]], patlen - j);
krz f0 2780000000 ; skip
krz f1 f5+4@ ; text
krz f2 f5+4294967200@ ; patlen
nta f2 f5+4294967192@ ; patlen - j
nta f5 4 krz f5@ f2
dro f3 2 ata f1 f3 dto f3 2 ; text+i
dro f1@ 2 ata f0 f1@ dto f1@ 2; skip + *(text + i)
nta f5 4 krz f5@ f0@
nta f5 4 inj f5@ xx qllo ata f5 12
ata f3 f0
krz f5+4294967196@ f3 ; i
krz xx while1

nll lus
krz f0 10000
krz xx f5@

nll lus2
krz f0 f3
krz xx f5@
;;;;;;;;BMsearch;;;;;;;;

;;;;;;;;qllo;;;;;;;;
'c'i
nll qllo
krz f1 f5+4@
krz f0 f5+8@
fi f1 f0 xolo malkrz f0 f1 ; if f1 >= f0 then f0 = f1
krz xx f5@
;;;;;;;;qllo;;;;;;;;

;;;;;;;;size;;;;;;;;
'c'i
nll size
krz f0 f5+4@
krz f1 0

nll loop
fi f0@ 4294967295 clo malkrz xx lus3
ata f0 4
ata f1 1
krz xx loop

nll lus3
krz f0 f1
krz xx f5@
;;;;;;;;size;;;;;;;;
